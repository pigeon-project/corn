//
// Created by intellij-pest on 2020-03-14
// corn
// Author: Lyzh
//

corn = ${ (sexpr ~ (WHITE_SPACE ~ sexpr)*)? }

sexpr =
  { quote
//   | pair
  | list
  | atom
  }

list =
 ${ "(" ~ WHITE_SPACE? ~ (sexpr ~ (WHITE_SPACE ~ sexpr)*)? ~ WHITE_SPACE? ~ ")"
  | "[" ~ WHITE_SPACE? ~ (sexpr ~ (WHITE_SPACE ~ sexpr)*)? ~ WHITE_SPACE? ~ "]"
}

pair =
  ${ "(" ~ WHITE_SPACE? ~ sexpr ~ WHITE_SPACE ~ "." ~ WHITE_SPACE ~ sexpr ~ WHITE_SPACE? ~ ")"
  | "[" ~ WHITE_SPACE? ~ sexpr ~ WHITE_SPACE ~ "." ~ WHITE_SPACE ~ sexpr ~ WHITE_SPACE? ~ "]"
}

quote = ${ "&" ~ sexpr }


atom =
  { nil
  | bool
  | char
  | raw_str
  | str
//   | rational
  | float
  | uint
  | int
  | sym
  }

nil = { "#nil" }

bool = { "#f" | "#t" }

char = {
    "'" ~ (!("'") ~ (escape | ANY)) ~ "'"
}

raw_str = ${ "r\"" ~ (!"\"" ~ ANY)* ~ "\""}
str = ${ "\"" ~ (escape | rstr)* ~ "\"" }
// uint = ${ digit ~ (digit | "_")* }
uint = ${ digit+ }
int = ${ ("+" | "-") ~ uint }
float = ${ (int | uint) ~ "." ~ uint }
// rational = ${ (float | uint | int) ~ "/" ~ (float | uint | int) }
sym = ${ sym_char+ }

escape =
  _{ "\\\\"
  | "\\'"
  | "\\\""
  | "\\t"
  | "\\r"
  | "\\n"
  }

rstr = _{ (!("\\" | "\"") ~ ANY)+ }

digit = _{ '0'..'9' }

sym_char = _{
  !(";"
  | "'"
  | "."
  | "\""
  | "/"
  | "&"
  | "#"
  | "("
  | ")"
  | "["
  | "]"
  | WHITE_SPACE) ~ ANY
  }

WHITESPACE =
  { " "
  | "\t"
  | NEWLINE
  }

COMMENT = {
    (";;;" | ";;" | ";") ~ (!"\n" ~ ANY)* ~ "\n"
}